// --------------------------------------------------------------------------------FBP

spriteset(ss_FBP_purchase, "src/FBP_BUY_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FBP_P_purchase, "src/FB_BUY_P_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FBP_01, "src/FB_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBP_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FB_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBP_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FB_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FBP_01_PUP, "src/FB_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBP_01_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FB_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBP_01_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FB_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FBP_02, "src/NP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FBP_02, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/NP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FBP_02, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/NP_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_FBP_01 {	loaded:  [ss_FBP_01];	loading: [ss_FBP_01];}
spritegroup sg_FBP_02 {	loaded:  [ss_FBP_02];	loading: [ss_FBP_02];}

switch (FEAT_ROADVEHS, SELF, swg_FBP_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FBP_purchase;    // Alap modell
    1: return ss_FBP_P_purchase;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, sgw_FBP_01, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FBP_01;    // Alap modell
    1: return ss_FBP_01_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw_FBP, position_in_consist ) {
    1: ss_toldat;
	2: sg_FBP_02;
    sgw_FBP_01;
}
switch (FEAT_ROADVEHS, SELF, sw1_FBP_ss, cargo_subtype) {
	0: sw_FBP;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_FBP_articulated, extra_callback_info1) {
    0..2: item_FBP;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_FBP_capacity, position_in_consist) {
	1: return 0;
	2: return 66;
	return 40;
}
switch (FEAT_ROADVEHS, SELF, sw_FBP_length, position_in_consist) {
	1: return 1;
	2: return 8;
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_FBP_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_FBP) {
	property {
        name:								string(STR_FBP_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1956,02,01);
        model_life:						20;
        vehicle_life:						52;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					72; //4*12
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								140 hp;
        weight:							20 ton;
        cargo_capacity:					106;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw1_FBP_ss;
		purchase:						swg_FBP_purchase;
		articulated_part:        		sw_FBP_articulated;
		length:                  		sw_FBP_length;
		cargo_subtype_text: 			sw_FBP_livery_text;
		cargo_capacity: 				sw_FBP_capacity;
		purchase_cargo_capacity:		return 106 / 3;
		cost_factor:					70 * parapuco;
		running_cost_factor:			78 * pararuco;
		additional_text: 				string(STR_FBP_ADAT);
    }
}
// --------------------------------------------------------------------------------FBP End
