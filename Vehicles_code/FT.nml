// --------------------------------------------------------------------------------FT

spriteset(ss_FT_purchase, "src/FBT_BUY_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FT_P_purchase, "src/FBT_BUY_P_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FT_01, "src/FB_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FT_01, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FB_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FT_01, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FB_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FT_01_PUP, "src/FB_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FT_01_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FB_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FT_01_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FB_PUP_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FT_02, "src/FBT_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FT_02, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FBT_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FT_02, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FBT_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FT_02_PUP, "src/FBT_PDO_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FT_02_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FBT_PDO_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FT_02_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FBT_PDO_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_FT_01 {	loaded:  [ss_FT_01];	loading: [ss_FT_01];}
spritegroup sg_FT_02 {	loaded:  [ss_FT_02];	loading: [ss_FT_02];}

switch (FEAT_ROADVEHS, SELF, swg_FT_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FT_purchase;    // Alap modell
    1: return ss_FT_P_purchase;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, sgw_FT_01, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FT_01;    // Alap modell
    1: return ss_FT_01_PUP;    // Pantonograf (fel)
}
switch (FEAT_ROADVEHS, SELF, sgw_FT_02, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FT_02;    // Alap modell
    1: return ss_FT_02_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw_FT, position_in_consist ) {
    1: ss_toldat;
	2: sgw_FT_02;
	3: ss_toldat;
    sgw_FT_01;
}
switch (FEAT_ROADVEHS, SELF, sw1_FT_ss, cargo_subtype) {
	0: sw_FT;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_FT_articulated, extra_callback_info1) {
    0..3: item_FT;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_FT_capacity, position_in_consist) {
	1: return 0;
	2: return 66;
	3: return 0;
	return 40;
}
switch (FEAT_ROADVEHS, SELF, sw_FT_length, position_in_consist) {
	1: return 1;
	2: return 2;
	3: return 8;
    return 8;
}
switch (FEAT_ROADVEHS, SELF, sw_FT_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_FT) {
	property {
        name:								string(STR_FT_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1956,06,01);
        model_life:						20;
        vehicle_life:						52;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					48; //4*12
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								280 hp;
        weight:							28 ton;
        cargo_capacity:					106;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw1_FT_ss;
		purchase:						swg_FT_purchase;
		articulated_part:        		sw_FT_articulated;
		length:                  		sw_FT_length;
		cargo_subtype_text: 			sw_FT_livery_text;
		cargo_capacity: 				sw_FT_capacity;
		purchase_cargo_capacity:		return 106 / 4;
		cost_factor:					70 * parapuco;
		running_cost_factor:			78 * pararuco;
		additional_text: 				string(STR_FT_ADAT);
    }
}
// --------------------------------------------------------------------------------FT End
