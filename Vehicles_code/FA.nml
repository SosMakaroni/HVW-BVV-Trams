// --------------------------------------------------------------------------------FA

spriteset(ss_FA_purchase, "src/FA_BUY_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FA_P_purchase, "src/FA_BUY_P_8bpp.png")		{tmpl_purchase()}
spriteset(ss_FA, "src/FA_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FA, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FA_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FA, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FA_4x_8bpp.png") {tmpl_1(4)}
spriteset(ss_FA_PUP, "src/FA_PUP_1x_8bpp.png")		{tmpl_1(1)}
alternative_sprites(ss_FA_PUP, ZOOM_LEVEL_IN_2X, BIT_DEPTH_8BPP, "src/FA_PUP_2x_8bpp.png") {tmpl_1(2)}
alternative_sprites(ss_FA_PUP, ZOOM_LEVEL_IN_4X, BIT_DEPTH_8BPP, "src/FA_PUP_4x_8bpp.png") {tmpl_1(4)}

spritegroup sg_FA {	loaded:  [ss_FA];	loading: [ss_FA];}

switch (FEAT_ROADVEHS, SELF, swg_FA_purchase, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return ss_FA_purchase;    // Alap modell
    1: return ss_FA_P_purchase;    // Pantonograf (le)
}
switch (FEAT_ROADVEHS, SELF, sgw_FA, [
    STORE_TEMP((getbits(extra_callback_info1, 8, 8) < 1 ? CB_FLAG_MORE_SPRITES : 0) | PALETTE_USE_DEFAULT, 0x100),
    getbits(extra_callback_info1, 8, 8)
    ]) {
    0: return sg_FA;    // Alap modell
    1: return ss_FA_PUP;    // Pantonograf (fel)
}

switch (FEAT_ROADVEHS, SELF, sw_FA, position_in_consist ) {
    1: ss_toldat;
    sgw_FA;
}
switch (FEAT_ROADVEHS, SELF, sw1_FA_ss, cargo_subtype) {
	0: sw_FA;
	CB_RESULT_NO_TEXT;
}
switch (FEAT_ROADVEHS, SELF, sw_FA_articulated, extra_callback_info1) {
    0..1: item_FA;
    CB_RESULT_NO_MORE_ARTICULATED_PARTS;
}
switch (FEAT_ROADVEHS, SELF, sw_FA_capacity, position_in_consist) {
	1: return 0;
	return 42;
}
switch (FEAT_ROADVEHS, SELF, sw_FA_length, position_in_consist) {
	1: return 8;
    return 2;
}
switch (FEAT_ROADVEHS, SELF, sw_FA_livery_text, cargo_subtype) {
	0: return string(STR_COLOUR_SCHEME_BUDAPEST);
	CB_RESULT_NO_TEXT;
}


item(FEAT_ROADVEHS, item_FA) {
	property {
        name:								string(STR_FA_NAME);
        climates_available:				bitmask(CLIMATE_TEMPERATE, CLIMATE_ARCTIC, CLIMATE_TROPICAL);
        introduction_date:				date(1904,01,01);
        model_life:						20;
        vehicle_life:						52;
        reliability_decay:				10;
        refittable_cargo_classes:		bitmask(CC_PASSENGERS);
        non_refittable_cargo_classes:	bitmask(CC_PIECE_GOODS, CC_EXPRESS, CC_REFRIGERATED);
        loading_speed:					24; //2*12
        sprite_id:							SPRITE_ID_NEW_ROADVEH;
        speed:								45 km/h;
        misc_flags:						bitmask(ROADVEH_FLAG_2CC, ROADVEH_FLAG_TRAM, ROADVEH_FLAG_SPRITE_STACK);
        refit_cost:						0;
        running_cost_base:				RUNNING_COST_ROADVEH;
        power:								48 hp;
        weight:							11 ton;
        cargo_capacity:					42;
		cargo_age_period:					30;
        sound_effect:						SOUND_ELECTRIC_SPARK;
    }
    graphics {
		default:                 		sw1_FA_ss;
		purchase:						swg_FA_purchase;
		articulated_part:        		sw_FA_articulated;
		length:                  		sw_FA_length;
		cargo_subtype_text: 			sw_FA_livery_text;
		cargo_capacity: 				sw_FA_capacity;
		purchase_cargo_capacity:		return 21;
		cost_factor:					35 * parapuco;
		running_cost_factor:			40 * pararuco;
		additional_text: 				string(STR_FA_ADAT);
    }
}
// --------------------------------------------------------------------------------FA End
